// This Pine Script™ code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
//© GreenDayTrade

// a = Define lookback period to be a range of beteen 16 - 50 bars
//  a2 = Define correction between the divergence in price
//     a3 = treack high closing price and MACD value
//         a4 = define divergence
//             a5 = execute
//             simplified

//      If a
//          then a2
//              then a3

//////////////////////////////////////////
//@version=5
indicator("MACD Divergence Detector", shorttitle="MACD Div", overlay=true)

// MACD Inputs
fastLength = input.int(12, title="Fast Length", minval=1)
slowLength = input.int(26, title="Slow Length", minval=1)
signalSmoothing = input.int(9, title="Signal Smoothing", minval=1)

[macdLine, signalLine, _] = ta.macd(close, fastLength, slowLength, signalSmoothing)

// Lookback Range for Highs
minLookback = input(16, title="Minimum Lookback Period")
maxLookback = input(50, title="Maximum Lookback Period")

// Function to check for bearish divergence within a lookback range
checkBearishDivergence() =>
    float highestPrice = na
    float highestMacd = na
    for i = minLookback to maxLookback
        if high[i] > highestPrice or na(highestPrice)
            highestPrice := high[i]
            highestMacd := macdLine[i]
    if close > highestPrice and macdLine < highestMacd
        true
    else
        false

// Function to check for bullish divergence within a lookback range
checkBullishDivergence() =>
    float lowestPrice = na
    float lowestMacd = na
    for i = minLookback to maxLookback
        if low[i] < lowestPrice or na(lowestPrice)
            lowestPrice := low[i]
            lowestMacd := macdLine[i]
    if close < lowestPrice and macdLine > lowestMacd
        true
    else
        false

// Plotting the divergence
plotshape(series=checkBearishDivergence(), title="Bearish Divergence", location=location.abovebar, color=color.rgb(255, 0, 0), style=shape.triangledown, size=size.small)
plotshape(series=checkBullishDivergence(), title="Bullish Divergence", location=location.belowbar, color=color.rgb(0, 255, 8), style=shape.triangleup, size=size.small)